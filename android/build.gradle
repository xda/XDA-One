apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'newrelic'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId 'com.xda.one'
        minSdkVersion 14
        targetSdkVersion 22
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        simple {
            ext.enableCrashlytics = false
        }
        full {
            ext.enableCrashlytics = true
        }
    }

    defaultPublishConfig "simpleDebug"

}

dependencies {
    compile 'com.google.android.gms:play-services-plus:7.0.0'
    compile 'com.google.android.gms:play-services-identity:7.0.0'
    compile 'com.google.android.gms:play-services-analytics:7.0.0'

    compile 'com.crashlytics.android:crashlytics:1.+'

    compile 'com.android.support:support-v4:22.1.0'
    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:cardview-v7:22.1.0'
    compile 'com.android.support:recyclerview-v7:22.1.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.fasterxml.jackson.core:jackson-annotations:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.2'

    compile 'com.squareup:otto:1.3.6'

    compile 'net.nightwhistler.htmlspanner:htmlspanner:0.5-SNAPSHOT'
    compile 'com.makeramen:roundedimageview:2.0.1'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.newrelic.agent.android:android-agent:4.273.4'

    compile fileTree(include: '*.jar', dir: 'libs')
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}