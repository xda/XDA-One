buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://download.crashlytics.com/maven'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'
    
    lintOptions {
    abortOnError false
    disable 'MissingTranslation', 'ExtraTranslation'
    }    

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId 'com.xda.one'
        minSdkVersion 14
        targetSdkVersion 19
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        simple {
            applicationId = android.defaultConfig.applicationId
            versionName = android.defaultConfig.versionName
            ext.enableCrashlytics = false
        }
        full {
            applicationId = android.defaultConfig.applicationId
            versionName = android.defaultConfig.versionName
            ext.enableCrashlytics = true
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url 'http://download.crashlytics.com/maven'
    }
}

dependencies {
    compile 'com.google.android.gms:play-services:5.0.89'

    compile 'com.crashlytics.android:crashlytics:1.+'

    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.android.support:appcompat-v7:19.1.0'

    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.squareup.retrofit:converter-jackson:1.6.1'
    compile 'com.squareup.picasso:picasso:2.3.3'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.2'

    compile 'com.squareup:otto:1.3.5'

    compile 'net.nightwhistler.htmlspanner:htmlspanner:0.5-SNAPSHOT'

    compile 'com.makeramen:roundedimageview:1.3.0'
    compile 'me.grantland:autofittextview:0.2.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.2'

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-io:commons-io:2.4'

    compile project(':libraries:cardview-v7')
    compile project(':libraries:recyclerview-v7')
    // compile 'com.android.support:cardview-v7:+'
    // compile 'com.android.support:recyclerview-v7:+'

    compile fileTree(dir: 'libs', include: '*.jar')
}

def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}
